// Maya: Rename Objects Dialog (Inspired by 3dsMax Rename Tool)
// By Carlos Breban
// Usage: 
// 	1. Select node(s)
//	2. Set desired options in dialog
//  3. Press "Rename" button

global proc Renamer (string $incNodes[], string $incBaseName, 
string $incPrefix, string $incSuffix, int $incPrefNum, int $incSufNum, 
int $startNum, int $stepNum){
	string $tmpName = "";
	if(size($incNodes)>0){
		for ($i = 0; $i < size($incNodes); ++$i){		
			$tmpName = "";	
			if($incBaseName != 0 && $incBaseName != "")		
				$tmpName = $incBaseName;
			else
				$tmpName = $incNodes[$i];		
		
			if($incPrefNum != 0){		
				if($incPrefNum<size($tmpName))		
					$tmpName = `substring $tmpName ($incPrefNum+1) (size($tmpName))`;
				}

			if($incPrefix != 0)				
				$tmpName = $incPrefix + $tmpName;
				
			if($incSufNum != 0)
				$tmpName = `substring $tmpName 1 (size($tmpName) - $incSufNum)`;		
				
			if($incSuffix != 0)	
				$tmpName = $tmpName + $incSuffix;

			if($stepNum != 0){
				string $stepNum = `python ("'%s0%dd' % ('%'," + $stepNum + ") % "+($startNum + $i))`;
				$tmpName = $tmpName + $stepNum;			
				}
				
			rename $incNodes[$i] ($tmpName);	
			}
		}
	}


if(`window -exists renameWin`)deleteUI renameWin; 
window -t "Rename Objects" renameWin;
columnLayout lay_col;
rowColumnLayout -nc 2 -cw 2 100 lay_rowCol;

checkBox -l "Base Name:" -value 1 chk_baseName;
textField  -enable true fld_baseName;

checkBox -l "Prefix:"  chk_prefix;
textField  -enable false fld_prefix;

checkBox -l "Remove First:" chk_removeFirst;
rowColumnLayout -nc 2 -cw 1 30 -cw 2 100 lay_rowCol_prefix; 
intField  -enable false fld_removeFirst;
text -l " digits" -al "left" txt_removeFirstDigits;
setParent lay_rowCol;

checkBox -l "Suffix:" chk_suffix;
textField -enable false suffixFld;

checkBox -l "Remove Last:" chk_removeLast;
rowColumnLayout -nc 2 -cw 1 30 -cw 2 100 lay_rowCol_suffix; 
intField -enable false fld_removeLast;
text -l " digits" -al "left" txt_removeLastDigits;
setParent lay_rowCol;

checkBox -l "Numbered:" chk_Numbered;
text -l "";

text -l "Start From:" -al "right" txt_StartFrom;
rowColumnLayout -nc 2 -cw 1 30 -cw 2 100 lay_rowCol_startFrom; 
intField -enable false fld_startFrom;
text "";
setParent lay_rowCol;

text -l "Step:" -al "right" txt_step;
rowColumnLayout -nc 2 -cw 1 30 -cw 2 100 lay_rowCol_step; 
intField -enable false -value 1 fld_stepNum;
text "";

setParent lay_col;
button -l "Rename" -width 190 btn_rename;

window -e -width 200 -height 200 -s true -mnb false -mxb false renameWin;
showWindow renameWin;


global proc string FieldQuery (string $incChkBox, string $incField){
	string $valReturn;
	if(`isValidUiName $incChkBox`){
		int $check = `checkBox -q -value $incChkBox`;
		if($check>0){
			if(`isValidUiName $incField`){
				if(`textField -exists $incField`)
					$valReturn = `textField -q -text $incField`;
				if(`intField -exists $incField`)
					$valReturn = `intField -q -value $incField`;
				}
			}
		else $valReturn = 0;	
		}
	return $valReturn;
	}


global proc FieldEnabler (string $incChkBox, string $incField){
	if(`isValidUiName $incChkBox`){
		int $check = `checkBox -q -value $incChkBox`;
		if($check>0){
			if(`isValidUiName $incField`){
				if(`textField -exists $incField`)
					$valReturn = `textField -e -text "" -enable true $incField`;
				if(`intField -exists $incField`)
					$valReturn = `intField -e -value 1 -enable true $incField`;
				setFocus $incField;	
				}
			}	
		else{
			if(`isValidUiName $incField`){
				if(`textField -exists $incField`)
					$valReturn = `textField -e -text "" -enable false $incField`;
				if(`intField -exists $incField`)
					$valReturn = `intField -e -value 0 -enable false $incField`;
				}
			}
		}
	}


global proc RenameUIQuery (){
	string $selection[] = `ls -sl`;
	string $baseName = `FieldQuery chk_baseName fld_baseName`; 
	string $prefix = `FieldQuery chk_prefix fld_prefix`;
	int $prefixVal = `FieldQuery chk_removeFirst fld_removeFirst`;
	string $suffix =`FieldQuery chk_suffix suffixFld`;
	int $suffixVal = `FieldQuery chk_removeLast fld_removeLast`;
	int $startVal = `FieldQuery chk_Numbered fld_startFrom`;
	int $stepNumVal = `FieldQuery chk_Numbered fld_stepNum`;
	Renamer $selection $baseName $prefix $suffix $prefixVal $suffixVal $startVal $stepNumVal;
	}


button -e -c RenameUIQuery btn_rename;
string $baseEnable = "FieldEnabler chk_baseName fld_baseName";
string $prefEnable = "FieldEnabler chk_prefix fld_prefix";
string $prefValEnable = "FieldEnabler chk_removeFirst fld_removeFirst";
string $suffEnable = "FieldEnabler chk_suffix suffixFld";
string $suffValEnable = "FieldEnabler chk_removeLast fld_removeLast";
string $startStepEnable = "FieldEnabler chk_Numbered fld_startFrom; FieldEnabler chk_Numbered fld_stepNum";
checkBox -e -onc $baseEnable -ofc $baseEnable chk_baseName;
checkBox -e -onc $prefEnable -ofc $prefEnable chk_prefix;
checkBox -e -onc $prefValEnable -ofc $prefValEnable chk_removeFirst;
checkBox -e -onc $suffEnable -ofc $suffEnable chk_suffix;
checkBox -e -onc $suffValEnable -ofc $suffValEnable chk_removeLast;
checkBox -e -onc $startStepEnable -ofc $startStepEnable chk_Numbered;